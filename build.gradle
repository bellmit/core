subprojects {

    version = '1.4.5'
    group = 'com.logginghub'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: "maven"

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// jshaw - support for "provided" dependencies until Gradle implement it out of the box
//
    configurations {
        provided
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
        test {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

    repositories {
        mavenCentral()
        maven {
            url "http://www.vertexlabs.co.uk/repository"
        }
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'org.mockito:mockito-all:1.9.5'
    }

}

