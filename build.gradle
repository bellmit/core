plugins {
    id 'org.hidetake.ssh' version '1.0.1'
}

subprojects {

    version = '1.4.8'
    group = 'com.logginghub'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'

    repositories {
        mavenCentral()
        maven {
            url "http://www.vertexlabs.co.uk/repository"
        }
    }

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// jshaw - support for "provided" dependencies until Gradle implement it out of the box
//
    configurations {
        provided
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
        test {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// jshaw - workaround to fix the intellij resource copying bug
//
    task copyTestResources(type: Copy) {
        from "${projectDir}/src/test/resources"
        into "${buildDir}/classes/test"
    }
    processTestResources.dependsOn copyTestResources

    task copyMainResources(type: Copy) {
        from "${projectDir}/src/main/resources"
        into "${buildDir}/classes/main"
    }
    processResources.dependsOn copyMainResources
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

    remotes {
        hosting {
            host = 'www.vertexlabs.co.uk'
            user = 'mavenrepo'
            identity = file(System.properties['user.home'] + "/.ssh/id_rsa")
        }
    }

    configurations {
        deployerJars
    }

    dependencies {
        deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
    }

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "scp://mavenrepo@www.vertexlabs.co.uk:/var/www/repository")  {
                authentication(userName: "mavenrepo", privateKey: System.properties['user.home'] + "/.ssh/id_rsa")
            }
        }
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'org.mockito:mockito-all:1.9.5'
    }

}

